<?xml version="1.0" ?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

	<xsl:variable name="source_schema"><xsl:value-of select="post/source_schema"/></xsl:variable><!--this way we'll be able to use these fields in comments template-->
	<xsl:variable name="source"><xsl:value-of select="post/source"/></xsl:variable>

	<xsl:template match="post">
	
		<xsl:variable name="subjectOrNull">
			<xsl:choose>
				<xsl:when test="subject"><xsl:value-of select="subject"/></xsl:when>
				<xsl:otherwise>No Subject</xsl:otherwise>
			</xsl:choose>
		</xsl:variable>
		
		<xsl:variable name="commentsCount">
			<xsl:value-of select="count(//comment[not(@state) or @state!='D'])"/> <!-- count all comments that either do not have state attribute or it is not equal to D(eleted) -->
		</xsl:variable>
		
		<!-- main interface color -->
		<xsl:variable name="interfaceColor">
			<xsl:choose>
				<xsl:when test="source_shortname='LiveJournal'">#87CEFA</xsl:when>
				<xsl:otherwise>#DCDCDC</xsl:otherwise>
			</xsl:choose>
		</xsl:variable>
		
		<html>
			<head>
				<title><xsl:value-of select="$subjectOrNull"/></title>
				<meta http-equiv="content-type" content="text/html; charset=utf-8" />
				<style>
					body {
						margin: 0;
						font-family: Arial,"Helvetica Neue",Helvetica,sans-serif;
						font-size:100%;
					}
					h1 {
						font-size: 1.5em;
						font-weight: bold;
					}
					
					ul {
						list-style: none;
					}
					
					li {
						margin: 5px 0;
					}
					
					#wrapper {
						margin: 0 50px 60px 50px;
					}
					
					#post-wrapper {
						margin: 40px 0 10px 0;
					}
					
					#comment-wrapper {
						margin: 0 0 10px 0;
					}
					
					#comment-wrapper > ul {
						margin-left: -40px;
					}
					
					#author, .username, .post-properties, .comment-author, .comment-subject {
						font-weight:bold;
					}
					
					#header {
						background-color: <xsl:value-of select="$interfaceColor"></xsl:value-of>;
						margin: 0;
						padding: 3px;
						text-align: center;
					}
					
					#comment-count {
						text-align: center;
						font-weight:bold;
						border-top: 1px solid <xsl:value-of select="$interfaceColor"></xsl:value-of>;
						border-bottom: 1px solid <xsl:value-of select="$interfaceColor"></xsl:value-of>;
						padding: 10px 0;
					}
					
					.comment-header {
						background-color: <xsl:value-of select="$interfaceColor"></xsl:value-of>;
						padding: 10px;
					}
					
					.comment-body {
						padding: 10px 0;
					}
					
					#event-body, .comment-body {
						white-space: pre-wrap;
					}
				</style>
			</head>
			<body>
				<div id="header">Generated by <xsl:value-of select="generator"/></div>
				<div id="wrapper">
					<div id="post-wrapper">
						<div>
							<xsl:element name="a"><xsl:attribute name="href"><xsl:value-of select="author_url"/></xsl:attribute><xsl:attribute name="id">author</xsl:attribute>
								<xsl:value-of select="author"/>
							</xsl:element> @ <xsl:element name="a"><xsl:attribute name="href"><xsl:value-of select="concat($source_schema, '://www.', $source)"/></xsl:attribute>
								<xsl:value-of select="source_shortname"/>
							</xsl:element> wrote on <xsl:value-of select="eventtime"/></div>
						<xsl:if test="current_mood"><div><span class="post-properties">Current mood: </span><xsl:value-of select="current_mood"/></div></xsl:if>
						<xsl:if test="current_music"><div><span class="post-properties">Current music: </span><xsl:value-of select="current_music"/></div></xsl:if>
						<xsl:if test="current_location"><div><span class="post-properties">Current location: </span> <xsl:value-of select="current_location"/></div></xsl:if>
						<xsl:if test="taglist">
							<div><span class="post-properties">Tags: </span> 
								<xsl:for-each select="taglist/tag">
									<xsl:element name="a"><xsl:attribute name="href"><xsl:value-of select="../../author_url"/>/tag/<xsl:value-of select="."/></xsl:attribute>
										<xsl:value-of select="."/>
									</xsl:element><xsl:if test="position() != last()">, </xsl:if>
								</xsl:for-each>
							</div>
						</xsl:if>
						<xsl:if test="security">
							<div><span class="post-properties">Security: </span> 
								<xsl:choose>
									<xsl:when test="security[text()='usemask']">Friends</xsl:when>
									<xsl:when test="security[text()='private']">Private</xsl:when>
									<xsl:otherwise><xsl:value-of select="security"/></xsl:otherwise>
								</xsl:choose>
							</div>
						</xsl:if>
						<h1>
							<xsl:element name="a"><xsl:attribute name="href"><xsl:value-of select="url"/></xsl:attribute>
								<xsl:value-of select="$subjectOrNull"/>
							</xsl:element>
						</h1>
						<div id="event-body"><xsl:value-of select="event" disable-output-escaping="yes"/></div>
					</div>
					
					<div id="comment-count"><xsl:value-of select="$commentsCount"/> comment<xsl:if test="$commentsCount!=1">s</xsl:if></div>
					<xsl:if test="$commentsCount>0">
						<div id="comment-wrapper">
							<ul><xsl:apply-templates select="comments/comment"/></ul>
						</div>
					</xsl:if>
				</div>
			</body>
			<script type="text/javascript">
				<!-- Can't get those under CDATA because xls stops processing its variables there. -->
				var schema = '<xsl:value-of select="$source_schema"></xsl:value-of>';
				var source = '<xsl:value-of select="$source"></xsl:value-of>';
						
				<!-- This javascript is here because we need to ensure that &lt;htmltag&gt;SOME TEXT&lt;/htmltag&gt; gets properly converted to <htmltag>SOME TEXT</htmltag> 
				even if some browsers (I'm looking at you, Firefox!) don't support disable-output-escaping="yes". The javascript is wrapped in CDATA because of element.innerHTML.indexOf('<'): angle brackets cause troubles in IE -->
				<![CDATA[
					window.onload = function() {
						var eventBody = document.getElementById('event-body');
						escapeSequencesToAngleBrackets(eventBody);
						convertRemoteLinksToLocalLinks(eventBody, 'img', 'src');
						convertRemoteLinksToLocalLinks(eventBody, 'a', 'href');
						expandLJTags(eventBody);
						var commentBodies = document.getElementsByClassName('comment-body');
						for(var i = 0; i < commentBodies.length; i++)
						{
							escapeSequencesToAngleBrackets(commentBodies[i]);
							expandLJTags(commentBodies[i]);
						}
					};
					
					function escapeSequencesToAngleBrackets(element) {
						//no angular brackets but &lt; present â€” looks like disable-output-escaping is not supported
						if(element.innerHTML.indexOf('<') == -1 && element.innerHTML.indexOf('&lt;') > -1)
						{
							var dummy = document.createElement('dummy');
							dummy.innerHTML = element.innerText; //this operation converts &lt;htmltag&gt;SOME TEXT&lt;/htmltag&gt; to <htmltag>SOME TEXT</htmltag> in dummy
							element.innerHTML = dummy.innerHTML;
						}
					};
					
					function convertRemoteLinksToLocalLinks(parentElement, tagToProcess, tagAttributeToRewrite)
					{
						var childrenOfType = parentElement.getElementsByTagName(tagToProcess);
						for(var i = 0; i < childrenOfType.length; i++)
						{
							var localScr = childrenOfType[i].getAttribute('data-local-src');
							if(localScr)
							{
								childrenOfType[i].setAttribute(tagAttributeToRewrite, localScr);
							}
						}
					};
					
					function expandLJTags(parentElement)
					{
						var ljTags = parentElement.getElementsByTagName('lj');
						var attributesToProcess = ['user', 'comm'];
						for(var i = 0; i < ljTags.length; i++)
						{
							for(var j = 0; j < attributesToProcess.length; j++)
							{
								var userOrCommAttr = ljTags[i].getAttribute(attributesToProcess[j]);
								if(userOrCommAttr)
								{
									var url = schema + '://' + userOrCommAttr.replace(/_/g, "-") + '.' + source;
									createAndInsertLink(url, userOrCommAttr, ljTags[i]);
									break;
								}
							}
						}
					};
					
					function createAndInsertLink(href, text, insertBeforeElement)
					{
						var userLink = document.createElement('a');
						userLink.setAttribute('class', 'username');
						userLink.setAttribute('href', href);
						userLink.appendChild(document.createTextNode(text));
						var p = insertBeforeElement.parentNode;
						p.insertBefore(userLink, insertBeforeElement);
					}
				]]>
			</script> 
		</html>
	</xsl:template>
	
	<xsl:template match="comments/comment">
		<li>
			<xsl:choose>
				<xsl:when test="@state='D'">
					<div class="comment-header">The comment by <span class="comment-author">
						<xsl:element name="a"><xsl:attribute name="href"><xsl:value-of select="@poster_url"/></xsl:attribute>
							<xsl:value-of select="@poster_name"/>
						</xsl:element></span> was deleted.</div>
				</xsl:when>
				<xsl:otherwise>					
					<div class="comment-header">
						<span class="comment-author"><xsl:element name="a"><xsl:attribute name="href"><xsl:value-of select="@poster_url"/></xsl:attribute>
							<xsl:value-of select="@poster_name"/>
						</xsl:element></span> wrote on <xsl:value-of select="date"/><xsl:if test="subject"> : <span class="comment-subject"><xsl:value-of select="subject"/></span></xsl:if>
					</div>
					<div class="comment-body">
						<xsl:value-of select="body" disable-output-escaping="yes"/>
					</div>
				</xsl:otherwise>
			</xsl:choose>
		</li>
		<ul>
			<xsl:apply-templates select="comments/comment"/>
		</ul>
	</xsl:template>
	
</xsl:stylesheet>